package funkin.game.system;

import funkin.backend.system.Parser;
import tjson.TJSON;

typedef Player =
{
	name:String,
	isPlayer:Bool,
	?isBopper:Bool,
	?hideStrumline:Bool
}

typedef ChartNote =
{
	strumTime:Float,
	noteData:Int,
	sustainLength:Float,
	character:Int,
	?noteType:String
}

typedef ChartEvent =
{
	event:String,
	values:Array<Dynamic>,
}

typedef ChartEventGroup = 
{
	events:Array<ChartEvent>,
	strumTime:Float
}

typedef Song =
{
	characters:Array<Player>,
	song:String,
	hasVoices:Bool,
	stage:String,
	bpm:Float,
	scrollSpeed:Float,
	notes:Array<ChartNote>,
	events:Array<ChartEventGroup>,
	keys:Int,
	postfix:String, // for unique Inst/Voices for each difficulties
	evoChart:Bool
}

typedef PsychSection =
{
	?sectionBeats:Int,
	?lengthInSteps:Int,
	sectionNotes:Array<Array<Dynamic>>,
	/* [0] strumTime, (float)
	 * [1] noteData, (int)
	 * [2] sustainLength, (float)
	 * [3] noteType (string, optional)
	 */
	typeOfSection:Int, // unused
	gfSection:Bool,
	altAnim:Bool,
	mustHitSection:Bool,
	changeBPM:Bool,
	bpm:Float
}

typedef PsychSong =
{
	player1:String, // boyfriend
	player2:String, // dad
	gfVersion:String, // gf
	notes:Array<PsychSection>,
	events:Array<Dynamic>,
		/* Float: strumTime
		 * Array<Array<String>>: arrays of events with values:
		 * [0] eventName
		 * [1] value1
		 * [2] value2
		 */
	splashSkin:String,
	song:String,
	needsVoices:Bool,
	arrowSkin:String,
	stage:String,
	validScore:Bool, // unused
	bpm:Float,
	speed:Float
}

class SongData
{
	public var chart:Song;

	public var songName:String;
	public var songPath:String;
	public var hasVoices:Bool;
	public var stage:String;
	public var bpm:Float;
	public var scrollSpeed:Float;
	public var keys:Int;
	public var postfix:String;
	public var characters:Array<Player>;

	public function new(songPath:String, ?difficulty:String)
	{
		difficulty ??= PlayState.getDifficulties(songPath)[0];

		final sourceData = Paths.chart(songPath, difficulty);
		final chartEngine = justifyEngine(sourceData);

		#if !web
		trace([sourceData, chartEngine]);
		#end

		var chart:Song;
		if (sourceData != null)
		{
			chart = Parser.chart(FileUtil.getContent(sourceData), chartEngine);

			if (chartEngine != EVOLUTION)
				Parser.saveJson('songs/$songPath/charts/$difficulty', chart);

			this.songPath = songPath;
			songName = chart.song;
			hasVoices = chart.hasVoices;
			stage = chart.stage;
			bpm = chart.bpm;
			scrollSpeed = chart.scrollSpeed;
			keys = chart.keys;
			postfix = chart.postfix;
			bpm = chart.bpm;
			characters = chart.characters;

			this.chart = chart;
		}
	}

	public static function justifyEngine(path:String):ChartEngineType
	{
		if (path != null)
		{
			if (path.endsWith('.fnfc'))
				return VSLICE;

			var jsonContent = FileUtil.getContent(path);
			if (jsonContent.contains('"evoChart": true'))
				return EVOLUTION;
			else if (jsonContent.contains('"validScore":'))
				return PSYCH;

			var json = TJSON.parse(jsonContent);
			if (Reflect.hasField(json, 'song') && !(json.song is String))
				if (Reflect.hasField(json.song, 'validScore'))
					return PSYCH_LEGACY;
				else
					return CODENAME;
		}
		return UNKNOWN;
	}
}
